/*
   Copyright 2023 SpecMesh Contributors (https://github.com/specmesh)

   Licensed under the Apache License, Version 2.0 (the "License"); you may not
   use this file except in compliance with the License. You may obtain a copy of
   the License at

   http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
   WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
   License for the specific language governing permissions and limitations under
   the License.
 */
plugins {
    id 'java'
    id 'idea'
    id 'checkstyle'
    id 'signing'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

group = 'io.specmesh.kafka-random-generator'
version = '0.5.0-SNAPSHOT'
archivesBaseName = 'kafka-random-generator'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.apache.avro:avro:1.11.3'
    implementation 'com.github.mifmif:generex:1.0.2'

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Avro Random Generator',
                'Implementation-Version': version
        )
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task standalone(type: Jar) {
    destinationDir file("$rootDir/bin")
    archiveName 'arg.jar'
    from configurations.runtimeClasspath
    manifest {
        attributes 'Main-Class': 'io.specmesh.avro.random.generator.Main'
    }
    with jar
}

clean {
    doLast {
        file("$rootDir/bin").deleteDir()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'kafka-random-generator'
            version = project.version

            pom {
                name = 'Kafka Random Generator'
                description = 'Kafka Random Generator is a tool for randomly generating data to match Avro schemas'
                url = 'https://github.com:specmesh/kafka-random-generator'

                scm {
                    url = 'https://github.com/specmesh/kafka-random-generator'
                    connection = 'scm:git:ssh://github.com:specmesh/kafka-random-generator.git'
                    developerConnection = 'scm:git:ssh://github.com:specmesh/kafka-random-generator.git'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'C0urante'
                        name = 'Chris Egerton'
                    }
                    developer {
                        id = 'blu3monkey'
                        name = 'NAvery'
                    }
                }
            }
        }
    }
}

signing {
    setRequired {
        !project.version.toString().endsWith("-SNAPSHOT") && !project.hasProperty("skipSigning")
    }
    if (project.hasProperty("signingKey")) {
        useInMemoryPgpKeys(properties["signingKey"].toString(), properties["signingPassword"].toString())
    }

    sign(publishing.publications["mavenJava"])
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/google_checks.xml")
//    toolVersion = '8.47'
}

javadoc {
    options {
        addBooleanOption('html5', true)
        addStringOption("Xwerror", "-quiet")
        addStringOption('Xdoclint:none', '-quiet')
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'https://avro.apache.org/docs/1.10.2/api/java/'
    }
}
